#!/usr/bin/perl -w -I.
#
# Overview
# --------
#
# This is a simple proof of concept which will allow a series of rules,
# provided as JSON-encoded objects, to identify malicious requests from
# webserver logs.
#
# In the event of a "hit" the remote IP will be output for each malicious
# request.  Regardless of the number of hits an IP will only be output once
# for each run.
#
#
# Usage
# -----
#
#  ./webserver-attacks logfile1.log logfile2.log
#
#  For more details add "--verbose".
#
#  TODO:  Handle installation.
#
#
# Rules
# -----
#
# Each rule consists of a regular expression to be matched against a part
# of the request.  For example:
#
#  * you might match "index.php" against the "path".
#
#  * You might match 404 against the "status".
#
# Each rule can consist of multiple header+regexp pairs, and for a match to
# be made each one must match.  As an example you might wish to find POST
# requests made to missing PHP files via this rule:
#
#    code:404 path:\.php method:POST
#
#
# Steve
# --
#



use strict;
use warnings;

use Apache::Log::Parser;
use Getopt::Long;
use JSON;




#
#  Get configuration options
#
my %CONFIG;
exit if ( !GetOptions( "verbose", \$CONFIG{ 'verbose' } ) );


#
#  The rules will be populated from the JSON files which are located
# beneath /etc/webserver.attacks.d, or ./webserver.attacks.d.
#
my @rules;


#
#  Load all the rules
#
loadRules();

if ( !scalar @rules )
{
    print "Failed to load rules - Please investigate\n";
    exit(1);
}

#
# The user will provide the logfiles.
#
if ( !scalar @ARGV )
{
    print "Usage: $0 logfile1 logfile2 .. logfileN\n";
    exit(1);
}

#
#  Process each named file.
#
foreach my $file (@ARGV)
{
    processLog($file);
}

exit 0;


=begin doc

Load the rule-files

=end doc

=cut

sub loadRules
{
    foreach my $dir (qw! /etc/webserver-attacks.d/ ./webserver-attacks.d/ !)
    {
        foreach my $rule ( sort( glob( $dir . "/*" ) ) )
        {
            if ( ( -e $rule ) && ( !-d $rule ) )
            {
                my $content;

                open( my $handle, "<", $rule ) or
                  die "Failed to open rule $rule - $!";
                while ( my $line = <$handle> )
                {
                    $content .= $line;
                }
                close($handle);

                my $obj = decode_json($content);

                if (@$obj)
                {
                    push( @rules, @$obj );
                }
            }
        }
    }
}



=begin doc

Process the specified logfiles looking for attacks.

=end doc

=cut

sub processLog
{
    my ($file) = (@_);

    my %seen;


    my $parser = Apache::Log::Parser->new( fast => 1 );

    open( my $handle, "<", $file ) or
      die "Failed to open $file - $!";

    while ( my $line = <$handle> )
    {

        #
        #  Parse the line
        #
        my $data = $parser->parse($line);


        #
        # For each rule we have
        #
        foreach my $rule (@rules)
        {

            #
            #  Did we either fail to parse an IP, or see this IP already?
            #
            next if ( ! $data->{ 'rhost' } || $seen{ $data->{ 'rhost' } } );

            #
            #  We assume each rule matches each line
            #
            my $match = 1;

            #
            #  The rules are hashes - with an extra "description"
            # key which is ignored.
            #
            foreach my $key ( keys %$rule )
            {

                #
                #  Skip the description.
                #
                next if ( $key eq "description" );

                #
                #  Skip if we've already ruled this ruleset out
                #
                next if ( !$match );


                #
                #  OK so we have the header and regexp we're goign to match from
                # the rule.
                #
                my $header = $key;
                my $regexp = $rule->{ $key };

                #
                #  Get the value of the given header from this request
                #
                my $value = $data->{ $header } || next;

                #
                #  Do we match?
                #
                if ( $value !~ /$regexp/i )
                {
                    $match = 0;
                }
            }


            #
            #  As a result of performing *all* the matches on this rule
            # we have either matched, or not.
            #
            #  If we matched then report that.
            #
            if ($match)
            {
                if ( $CONFIG{ 'verbose' } )
                {
                    my $desc = $rule->{ 'description' } || "No description";

                    # show the matching values.
                    print "$desc - INPUT ";

                    # show each parsed field/value
                    foreach my $key ( sort keys %$data )
                    {
                        print $key . ":" . $data->{ $key } . " ";
                    }

                    # newline.
                    print "\n";
                }

                # prevent rematching this line.
                $seen{ $data->{ 'rhost' } } += 1 if ( $data->{'rhost'} );

                # show the IP.
                print $data->{ 'rhost' } . "\n";
            }
        }
    }

    close($handle);
}




